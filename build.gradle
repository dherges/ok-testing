plugins {
  id "java"
  id "application"
}


repositories {
  jcenter()
  mavenCentral()
  maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}


configurations {
  e2eTestCompile.extendsFrom testCompile
  e2eTestRuntime.extendsFrom testRuntime
}


sourceSets {
  e2eTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/test-e2e/java')
    }
    resources.srcDir file('src/test-e2e/resources')
  }
}



dependencies {
  compile 'com.sparkjava:spark-core:2.5'

  compile 'com.squareup.retrofit2:retrofit:2.1.0'
  compile 'com.squareup.retrofit2:converter-moshi:2.1.0'
  compile 'com.squareup.okhttp3:okhttp:3.4.1'
  compile 'com.squareup.okio:okio:1.9.0'
  compile 'com.squareup.moshi:moshi:1.2.0'

  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:3.5.2'
  testCompile 'com.squareup.okhttp3:mockwebserver:3.4.1'

  e2eTestCompile 'info.cukes:cucumber-java8:1.2.5-SNAPSHOT'
  e2eTestCompile 'info.cukes:cucumber-junit:1.2.5-SNAPSHOT'
  e2eTestCompile 'com.orhanobut:mockwebserverplus:0.8.2-SNAPSHOT'
}



/** e2e tasks **/

task e2eTest(type: Test) {
  testClassesDir = sourceSets.e2eTest.output.classesDir
  classpath = sourceSets.e2eTest.runtimeClasspath
}

configure(e2eTest) {
  group = 'verification'
  description = 'Runs end-to-end test cases.'
}

check.dependsOn e2eTest
e2eTest.mustRunAfter test

tasks.withType(Test) {
  reports.html.destination = file("${reporting.baseDir}/${name}")
}



/** java application distributable */

group 'oktesting'
version '1.0-SNAPSHOT'

jar {
  manifest {
    attributes 'Main-Class': 'oktesting.app.AppBootstrap'
  }
}

sourceCompatibility = 1.8
mainClassName = 'oktesting.app.AppBootstrap'
